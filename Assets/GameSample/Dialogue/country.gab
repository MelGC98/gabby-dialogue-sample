// This sample demonstrates localization with an imaginary language Camilla learned.
// The game-side scripts for this sample allow changing the overall language, which isn't something a game would typically do.
// However, it's useful for demonstrating localized text more naturally in the sample.
gabby 0.2
language english

[Camilla.Country]
    (Camilla, happy)   ...and really, it was one of the most beautiful places I've ever seen!
    (Camilla, sad)     To think, if it weren't for that shipwreck I wouldn't have ever found it...
    (Camilla, annoyed) Oh, and also that swordfight with the pirates.
    (Camilla, happy)   But that stuff's boring!

    (Player) Wait, I want to hear about the pi-

    (Camilla, very happy) Boring!
    <happy>
    +                     What's interesting about the story is that while I was there, I found this beautiful city.
    (Camilla, happy)      It looked ancient! The roads were paved with large, smooth cobblestone, and the buildings were engraved with runes and inlaid with gold.
    -                     And everyone spoke an ancient language. And they taught it to me!
    (Camilla, very happy) So, here begins your lesson in speaking <color=#fcd303>Gabarian</color>!

    * This sample demonstrates localization.
    * The language will be set to Gabarian for the duration of the next dialogue, and then it will be repeated in English.

    - So, Gabarian is pretty simple...
    > setLanguage("gabarian")
    >> Camilla.LocalizedDialogueExample

// The Gabarian version of this dialogue sets the language back to English and links here
[Camilla.LocalizedDialogueExample]
    - I just said, "This is how you say hello!"
    - "Isn't that neat?"
    <very happy>
    - "It only took me a few weeks to become fluent!"
    >> Camilla.CountryEnd

[Camilla.CountryEnd]
    * She stops and thinks for a moment, looking conflicted.
    - Well, you know...
    - This is fun and all, but I guess it would be tiresome to try to teach you a whole language.
    - So just remember this:
    - If you want to say hello in Gabarian, just say <color=#fcd303>salwei</color>!
