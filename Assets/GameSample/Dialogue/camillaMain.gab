// This is the main file for Camilla's dialogue.
// It acts as the main entry point for all of her dialogues, linking to many of the other dialogue files in this sample.
gabby 0.2
language english

[Camilla.Main]
    if isNot(camilla.introduced, true)
        >> Camilla.Introduction
    else if isEqual(camilla.angry, true)
        >> Camilla.AngryGreeting
    else if isQuestActive(lampQuest)
        >> Camilla.LampQuest_Main
    else if showRandomly(0.02) // Rare greeting, 1 in 50
        >> Camilla.Greeting4
    else
        if showRandomly(0.3333)
            >> Camilla.Greeting1
        else if showRandomly(0.3333)
            >> Camilla.Greeting2
        else
            >> Camilla.Greeting3

[Camilla.Introduction]
    - Hey, I'm <b>Camilla</b>!
    <very happy>
    + It's nice to meet you.
    <happy>
    - Talk to me again for examples of Gabby in action!
    <annoyed>
    - Or you can talk to Charles if you want to learn more about the sample.
    > set(camilla.introduced, true)

[Camilla.Greeting1]
    <very happy>
    - Oh hey!
    >> Camilla.SelectSample

[Camilla.Greeting2]
    - 'Sup?
    >> Camilla.SelectSample

[Camilla.Greeting3]
    <very happy>
    - Heya, Player!
    >> Camilla.SelectSample

[Camilla.Greeting4]
    // This is a one off thing, so just use Greeting1 if we've shown it before
    if isEqual(camilla.hasShownRareGreeting, true)
        >> Camilla.Greeting1
    > set(camilla.hasShownRareGreeting, true)

    <very sad>
    - Hey.
    <sad>
    - Hey, actually, can I ask you something?
    - Do you think Charles and I will ever be in a real game?
    <very sad>
    + Just being a sample character kind of sucks...
    : You definitely will!
        (Player)            Yeah, you definitely will be!

        (Camilla, sad)      Yeah... I don't know about that, Player...
        (Camilla, happy)    I hope so though!
    : Probably not...
        (Player)            Uh... Probably not...

        (Camilla, very sad) Yeah, I don't think so either...
        (Camilla, angry)    It's like we're just throwaway characters! It's not fair!
        * She sighs in frustration, turning her back to you.
        + You decide to give her some time to herself.
        > fadeOutAndIn()
        end
    (Camilla, happy) Well, anyway... You came here to talk, right?
    >> Camilla.SelectSample

// This is the primary dialogue block, linking to all the other sample dialogues
// This dialogue also demonstrates a method of marking dialogues as "seen", implemented by this sample.
[Camilla.SelectSample]
    - Wanna hear a story, or ask me something? Or just chat?
    <very happy>
    - I'm up for anything!
    : Let's chat!
        (Player) Ok, let's chat!
        > fadeOut()
        * Camilla looks happy to have someone to talk to.
        + Before long, she's chatting away about...
        // TODO revisit this once option tags are supported
        // <seenTag:"Camilla.SelectSample.dungeon">
        : One of her many dungeon delving stories.
            // Don't set this one as seen, since it's random
            > fadeIn()
            >> Camilla.Dungeon
        : A far away country that you're not really sure actually exists.
            > setChatSeen("Camilla.SelectSample.country")
            > fadeIn()
            >> Camilla.Country
        : A strange recipe she learned during her travels.
            > setChatSeen("Camilla.SelectSample.cooking")
            > fadeIn()
            >> Camilla.Cooking
        : A fun game "some kid" taught her.
            > setChatSeen("Camilla.SelectSample.game")
            > fadeIn()
            >> Camilla.NimGame
        : Literally just that lamp in the corner.
            > setChatSeen("Camilla.SelectSample.lamp")
            > fadeIn()
            >> Camilla.Lamp
        : This one time she came across a "kinda shady" street vendor.
            > setChatSeen("Camilla.SelectSample.vendor")
            > fadeIn()
            >> Camilla.Vendor
        : How annoying Charles is.
            > setChatSeen("Camilla.SelectSample.charlesComplaints")
            > fadeIn()
            >> Camilla.CharlesComplaints
        : Honestly, you're not even sure.
            // Just choose one randomly
            // TODO Need a better way to choose a random block uniformly
            if showRandomly(0.125)
                > setChatSeen("Camilla.SelectSample.dungeon")
                > fadeIn()
                >> Camilla.Dungeon
            else if showRandomly(0.125)
                > setChatSeen("Camilla.SelectSample.country")
                > fadeIn()
                >> Camilla.Country
            else if showRandomly(0.125)
                > setChatSeen("Camilla.SelectSample.charlesComplaints")
                > fadeIn()
                >> Camilla.CharlesComplaints
            else if showRandomly(0.125)
                > setChatSeen("Camilla.SelectSample.cooking")
                > fadeIn()
                >> Camilla.Cooking
            else if showRandomly(0.125)
                > setChatSeen("Camilla.SelectSample.game")
                > fadeIn()
                >> Camilla.NimGame
            else if showRandomly(0.125)
                > setChatSeen("Camilla.SelectSample.lamp")
                > fadeIn()
                >> Camilla.Lamp
            else
                > setChatSeen("Camilla.SelectSample.vendor")
                > fadeIn()
                >> Camilla.Vendor
    : I've gotta go...
        (Player) Actually, I've gotta go...

        (Camilla, sad) Oh, ok...
        <happy>
        - No problem.
        <sad>
        - I mean, that's kinda weird though, right?
        <annoyed>
        - Because you're the one who walked over here and started talking to me.
        <annoyed>
        - Don't you think that's kinda rude?

        * You slowly back away, sensing that this will go on for a while.

        <angry>
        - Oh, fine! Go already!~
