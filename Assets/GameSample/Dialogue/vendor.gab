// This sample demonstrates a very basic dialogue based shop.
gabby 0.2
language english

[Camilla.Vendor]
    - ...and I found myself at this amazing, super old looking shop.
    - It was like, totally full of antiques and artifacts.
    <very happy>
    - It was really cool!
    <happy>
    - I think I had, say, 250 coins...
    - It wasn't much, but I could buy a few things with it!

    - So, I looked around, checking out all his stuff.
    - Among everything, there were a few items that really stood out to me.

    * She starts to recount her story, her words transporting you into another world.
    > fadeOut()

    (Camilla) Wow, this store is really amazing!
    -         You've got so many cool looking artifacts.

    (Vendor) I hope you can find something to your liking!

    > set("camilla.vendor.gold", 250)
    > showGold()
    >> Camilla.VendorMain

[Camilla.VendorMain]
    * What did Camilla buy?
    : This really cool, fancy looking sword!
        (Camilla, very happy) Ooh, I wanna buy that cool sword! How much is it?

        (Vendor) This sword belonged to The Great King, Wuh... Wen...

        (Camilla, annoyed) Ok, yeah, I forget his name. But he was like a king or something... Anyway.

        (Vendor) This sword belonged to the king. It's a very rare artifact, and, like, incredibly well forged!
        -        Anyway, yeah, I'm asking for 75 coins.

        > set("camilla.vendor.item", "Sword")
        > set("camilla.vendor.cost", 75)
        >> Camilla.VendorPurchase
        // TODO use dialogue insertion here

    : Like, a really old looking amulet, with big gems and stuff.
        (Camilla, very happy) Oh! Look at this amulet!

        (Vendor) Yes, beautiful isn't it?

        (Camilla, happy) How much is it?

        (Vendor) That amulet is enchanted!
        -        It's said that whoever wears it will be blessed with good fortune.
        -        So you'll surely make back the mere 100 coins I'm asking for it very quickly!

        > set("camilla.vendor.item", "Amulet")
        > set("camilla.vendor.cost", 100)
        >> Camilla.VendorPurchase

    : I think it was actually the Staff of Ra, no joke!
        (Camilla, very happy) Hey, is that the Staff of Ra?

        (Vendor) The one and only!
        -        This fine artifact will only run you 250 gold.

        (Camilla, annoyed) Is it real?

        (Vendor) Like I said, it's the one and only!

        (Camilla, very happy) Wow!~ I WANT IT!
        (Camilla, sad) But it'll cost me all my gold...

        > set("camilla.vendor.item", "Staff")
        > set("camilla.vendor.cost", 250)
        >> Camilla.VendorPurchase

    : A bunch of old coins, kind of lame honestly.
        (Camilla, bored) So like, are these just some old coins that were buried for a long time or something?

        (Vendor) Ah, these are indeed coins from an ancient civilization!
        -        They were found during an archaeological dig years ago.

        (Camilla, bored) Having a hard time sellin' 'em?

        (Vendor) Ah!~ Er..

        (Camilla, bored) I'll offer 25 coins...
        <annoyed>
        +                And that's me being generous!

        (Vendor) Uh, sure!
        +        I suppose...

        > set("camilla.vendor.item", "Coins")
        > set("camilla.vendor.cost", 25)
        >> Camilla.VendorPurchase

    : Something that looked kind of like an alien weapon!
        (Camilla, very happy) Whoa!

        (Vendor) Interested in the... uh, alien weapon, are you?

        (Camilla, sad) Heh, I forget what he actually called it at the time...
        (Camilla, sad) Sorry...

        (Vendor) Interested in this thing, are you?
        -        Well, you have a good eye!
        -        This item is one of a kind.
        +        In fact, it's not even of this world!
        -        I'll take no less than 300 coins for it!

        (Camilla, annoyed) What?! But... I can't afford that!
        (Camilla, angry)   Give me a deal!

        (Vendor) I'm afraid I cannot.

        (Camilla, sad) Lame...

        (Camilla, angry)      But seriously! It was so lame! He wouldn't budge on the price at all.
        (Camilla, sad)        I bet I could've blown up so many things with it...
        (Camilla, very happy) Anyway! Back to my story!

        (Camilla, sad) What about...
        >> Camilla.VendorMain

    : Some old maps...
        (Camilla, very happy) Are these ancient treasure maps?

        (Vendor) They are indeed!
        -        Who knows what incredible treasure can be found with these!
        -        I'm asking 50 coins for all of them.

        > set("camilla.vendor.item", "Maps")
        > set("camilla.vendor.cost", 50)
        >> Camilla.VendorPurchase

    : I forget, but there was something else!
        (Vendor) Ah! So, you're interested in...
        -        You're interested... in...
        +        Uhh...

        (Camilla, angry) Ugh! Why'd you pick that one!
        +                I told you I forget!
        -                It was like, lame or something anyway.

        (Vendor) That's not for sale!
        -        Choose something else.

        >> Camilla.VendorMain
    : Nothing, I'm done!
        (Vendor) Alright, well, see ya!

        (Camilla, annoyed) Wait, no, he was more polite...

        (Vendor) Ah! Well, thank you, and do come again!

        (Camilla, very happy) I will, thanks a bunch!
        > hideGold()
        > fadeIn()
        if isEqual(camilla.vendor.gold, 250)
            >> Camilla.VendorEndingBoughtNothing
        else
            >> Camilla.VendorEnding

[Camilla.VendorPurchase]
    * Do you want to buy it?
    : Yes!
        if canAffordItem()
            > purchaseItem()
            (Camilla, very happy) I'll take it!
            >> Camilla.VendorMain
        else
            if isEqual("camilla.vendor.gold", 0)
                * Camilla reaches into her pockets, realizing she's out of gold.
                (Camilla, sad) Oh... Whoops, I'm out of coins!
                (Camilla, happy) I'll have to come back!
            else
                * Camilla empties her pockets, offering all of her coins to the vendor.
                (Vendor) I'm afraid that's not enough coins...

                (Camilla, sad) Oh... You're right. Ok.

    : No way!
        (Camilla, bored) Nah.

    + What about...
    >> Camilla.VendorMain

[Camilla.VendorEnding]
    (Camilla, very happy) So in the end, I bought some really cool stuff!

    // Note that this list isn't perfect as it won't show doubles of items
    if isTrue(camilla.boughtStaff)
        (Camilla, very happy) I got THE Staff of Ra!
        +                     It doesn't get any cooler than that!
        (Camilla, happy)      Pretty cool story, huh?
        end
    else
        if isTrue(camilla.boughtSword)
            (Camilla, very happy) I got a fancy looking sword...

        if isTrue(camilla.boughtAmulet)
            (Camilla, happy)      I got this really old looking amulet...
            (Camilla, annoyed)    And it's enchanted or something?

        if isTrue(camilla.boughtMaps)
            (Camilla, happy)      I got some old maps...

        if isTrue(camilla.boughtCoins)
            (Camilla, sad)        I got uh, some coins...

    (Camilla, very happy) And that's it!
    (Camilla, happy)      And then I just went home.
    +                     Pretty cool story, huh?

[Camilla.VendorEndingBoughtNothing]
    (Camilla, annoyed)    So in the end, I didn't end up buying anything...
    (Camilla, angry)      But it was all junk!
    (Camilla, very happy) And at least I saved money!
    <happy>
    +                     Pretty cool story, huh?
