// This contains all the standard inspect dialogue shown when interacting with an object.
gabby 0.2
language english

[Inspect.Refrigerator]
    * The fridge looks really shiny!
    * Inside, there's a lot of fresh ingredients and containers of leftover food.

[Inspect.Stove]
    * The stove looks like it's been cleaned recently.
    * There are grease stains on the floor in front of it though... Seems like something big spilled!

[Inspect.Cabinet1]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * I don't see any lightbulbs here, just kitchen gadgets...
        end

    * It's filled with kitchen gadgets.
    * Is that a strawberry huller? Huh.

[Inspect.Cabinet2]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * Just cutlery, nothing else.
        end

    * Looks like a bunch of cutlery in this drawer.
    * Pretty new looking, too!

[Inspect.Cabinet3]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * No lightbulbs here!
        end
        
    * There are pots and pans of all shapes and sizes in here.

[Inspect.Cabinet4]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * I feel like I've looked everywhere!
        end

    * Hmm... Is it rude of me to be snooping?
    
[Inspect.Sink]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * You take a look under the sink.
        >> LampQuest.FoundLightbulb

    * I can see my reflection in the faucet.

[Inspect.Table]
    * I think they got this table at UKEA.

[Inspect.Cactus]
    * It's a happy little cactus!

[Inspect.BackDoor]
    * This goes to their backyard.
    * I feel as if going outside would just make extra work for someone, so I'd better not.

[Inspect.FrontDoor]
    * I could leave if I wanted.
    * Maybe later...
    * Do you want to quit?
    : Yes
        (Charles, very happy) Thanks for stopping by!
        (Camilla, very happy) See you again soon!
        > fadeOut()
        > loadScene("Main")
    : No

[Inspect.Stairs]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * Camilla would have told me if they were downstairs, right?
        * I'd better stay up here.
        end

    * These are the stairs to go down to the basement.
    * I think it would be best if I stayed up here!

[Inspect.Bookshelf]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * Hmm... No lightbulbs here.
        end

    if isEqual(bookshelf.bookIndex, 1)
        > set(bookshelf.bookIndex, 2)
        >> Inspect.Bookshelf2
    else if isEqual(bookshelf.bookIndex, 2)
        > set(bookshelf.bookIndex, 3)
        >> Inspect.Bookshelf3
    else if isEqual(bookshelf.bookIndex, 3)
        > set(bookshelf.bookIndex, 4)
        >> Inspect.Bookshelf4
    else if isEqual(bookshelf.bookIndex, 4)
        > set(bookshelf.bookIndex, 5)
        >> Inspect.Bookshelf5
    else if isEqual(bookshelf.bookIndex, 5)
        > set(bookshelf.bookIndex, 6)
        >> Inspect.Bookshelf6
    else if isEqual(bookshelf.bookIndex, 6)
        > set(bookshelf.bookIndex, 7)
        >> Inspect.BookshelfEnd
    else
        > set(bookshelf.bookIndex, 1)
        >> Inspect.Bookshelf1

[Inspect.Bookshelf1]
    * Wow, they have a lot of books and games.

[Inspect.Bookshelf2]
    * "The Great Gabby".

[Inspect.Bookshelf3]
    * "To Kill a Manticore".

[Inspect.Bookshelf4]
    * "Ion Gabby".

[Inspect.Bookshelf5]
    * "Nineteen Eighty Four 2: Big Brother and You".

[Inspect.Bookshelf6]
    * "Gabby Invicta".

[Inspect.BookshelfEnd]
    * There must be about a hundred others on the shelves!

[Inspect.TV]
    * That's a big TV!
    * They've got a bunch of game consoles hooked up to it, too.

[Inspect.Couch]
    * Looks pretty comfy.

[Inspect.Painting]
    * It looks like a family portrait.
    * I guess those kids are Charles and Camilla?

[Inspect.Lamp]
    // Special dialogue for lamp quest
    if isQuestStageEqual(lampQuest, searchForLightbulb)
        * I guess it was its time.
        end
    else if isQuestStageEqual(lampQuest, giveCamillaLightbulb)
        * I should talk to Camilla.
        end
    else if isQuestStageEqual(lampQuest, replaceLightbulb)
        >> LampQuest.ReplaceLightbulb
        end
    else if isQuestStageEqual(lampQuest, returnToCamilla)
        * It's shining bright again!
        end
    * Looks like a pretty reliable lamp!

[Inspect.Window]
    * This window lets in a lot of light.
    * There's an ice cream truck across the street!
